#!/usr/bin/env jimsh

package require tcltest

source ../lib/getopt.tcl

proc parse-error {msg} {
	return -code error $msg
}

proc dict-sort {dict} {
	set result {}
	foreach k [lsort [dict keys $dict]] {
		lappend result $k [dict get $dict $k]
	}
	return $result
}

proc getopt-test {opts argv} {
	lassign [getopt-core $opts $argv] vars nargv
	list [dict-sort $vars] $nargv
}

test getopt-1.1 {bool, val, extra} {
	getopt-test {--no|stage --install: filename args} {--stage --nostage --install=abc test.c extra1 extra2}
} {{filename test.c install abc stage 0} {extra1 extra2}}

test getopt-1.2 {simpl bool options} {
	getopt-test {--stage --foo --bar} {--stage --bar}
} {{bar 1 foo 0 stage 1} {}}

test getopt-1.3 {bool options} {
	getopt-test {--no|stage --no|foo --bar} {--stage --nostage --stage --foo}
} {{bar 0 foo 1 stage 1} {}}

test getopt-1.4 {error - too few named options} -body {
	getopt-test {foo bar} {}
} -returnCodes error -result {No value supplied for foo}

test getopt-1.5 {error - too many options} -body {
	getopt-test {foo bar} {FOO BAR BAZ}
} -returnCodes error -result {Too many parameters supplied}

test getopt-1.6 {extra options with args} {
	getopt-test {foo bar args} {FOO BAR BAZ}
} {{bar BAR foo FOO} BAZ}

test getopt-1.7 {list options} {
	getopt-test {--foo:: --bar::} {--foo=1 --foo=2}
} {{foo {1 2}} {}}

test getopt-1.8 {list options with args} {
	getopt-test {--foo:: --bar:: args} {--foo=1 --foo=2 baz}
} {{foo {1 2}} baz}

test getopt-1.9 {list options with args} {
	getopt-test {--warn --dirs --all --recursive --exclude:: args} {--dirs test[0-9a]}
} {{all 0 dirs 1 recursive 0 warn 0} {{test[0-9a]}}}

test getopt-1.10 {list option given twice, last one wins} {
	getopt-test {--single: --multi::} {--single=a --single=b --multi=c --multi=d}
} {{multi {c d} single b} {}}

test getopt-1.11 {list options omitted} {
	getopt-test {--single: --multi::} {}
} {{} {}}

test getopt-1.12 {list options, omitted} -body {
	getopt-test {--single: --multi::} {--multi}
} -returnCodes error -result {Option --multi requires a parameter}

test getopt-1.13 {default value for multi} {
	getopt-test {--strip::none} {--strip --strip=bin --strip=lib}
} {{strip {none bin lib}} {}}

test getopt-1.14 {list option with default} {
	getopt-test {--single:set} {--single}
} {{single set} {}}

test getopt-1.15 {list option with default, overridden} {
	getopt-test {--single:set} {--single=other}
} {{single other} {}}

test getopt-1.16 {multi list option with default} {
	getopt-test {--multi::set} {}
} {{} {}}

test getopt-1.17 {multi list option with default} {
	getopt-test {--multi::set} {--multi}
} {{multi set} {}}

test getopt-1.18 {multi list option with default} {
	getopt-test {--multi::set} {--multi=a --multi --multi=b}
} {{multi {a set b}} {}}

test getopt-1.19 {bool with default} {
	getopt-test {--foo=1} {}
} {{foo 1} {}}

test getopt-1.20 {bool with default} {
	getopt-test {--foo=1} {--foo}
} {{foo 1} {}}

test getopt-1.21 {bool with default} {
	getopt-test {--no|foo=1} {}
} {{foo 1} {}}

test getopt-1.22 {bool with default} {
	getopt-test {--no|foo=1} {--nofoo}
} {{foo 0} {}}

test getopt-1.23 {list option with alternatives} {
	getopt-test {--strip --striplib} {--strip}
} {{strip 1 striplib 0} {}}

test getopt-1.24 {list option with alternatives} {
	getopt-test {--strip --striplib} {--striplib}
} {{strip 0 striplib 1} {}}

test getopt-1.25 {list option with alternatives} {
	getopt-test {--strip --striplib} {}
} {{strip 0 striplib 0} {}}

testreport
