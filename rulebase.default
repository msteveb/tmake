# vim:set syntax=tcl:
# These are the built-in rules
#
# They can be replaced if necessary

# ==================================================================
# Default variable settings and rules
# ==================================================================

# If settings are loaded via Load, these should all be ignored
# Thus we use define?
define? CCACHE ""
define? CC cc
define? CC_FOR_BUILD cc
define? CXX c++
define? AR ar
define? RANLIB ranlib
define? STRIP strip
define? ARFLAGS cr
define? SH_LINKFLAGS ""
define? CFLAGS ""
define? CXXFLAGS ""
define? LDFLAGS ""
define? HOSTCFLAGS ""

define? SH_CFLAGS ""
define? SH_LDFLAGS ""
define? SH_SOEXT .so
define? SHOBJ_CFLAGS ""
define? SHOBJ_LDFLAGS ""


define? LOCAL_LIBS ""
define? DESTDIR ""
define? OBJCFLAGS ""
define? INCPATHS ""

# These are set by the project via CFlags, ObjectCFlags, LinkFlags, etc.
define C_FLAGS ""
define CXX_FLAGS ""
define LD_FLAGS ""

# XXX Should be $TOP/publish
define PUBLISH publish

define PROJLIBS ""
define SYSLIBS ""

# Default header pattern for C and C++ dynamic dependencies
#
define HDRPATTERN {^[\t ]*#[\t ]*include[\t ]*[<\"]([^\">]*)[\">]}

set tmake(testruncount) 0
set tmake(testpasscount) 0

proc ObjectRule.c {obj src} {
	# Capture the current value of C_FLAGS and INCPATHS
	target $obj -inputs $src -msg {note Cc $target} -getvars C_FLAGS INCPATHS -do {
		run $CCACHE $CC $C_FLAGS $CFLAGS -c $inputs -o $target
	} -dyndep {
		header-scan-regexp-recursive $INCPATHS "" $HDRPATTERN
	}
}

proc ObjectRule.cpp {obj src} {
	# Capture the current value of CXX_FLAGS and INCPATHS
	target $obj -inputs $src -msg {note C++ $target} -getvars CXX_FLAGS INCPATHS -do {
		run $CCACHE $CXX $CXX_FLAGS $CXXFLAGS -c $inputs -o $target
	} -dyndep {
		header-scan-regexp-recursive $INCPATHS "" $HDRPATTERN
	}
}

# ==================================================================
# PROLOG/EPILOG HOOKS
# ==================================================================
proc BuildSpecProlog {} {
	# Local phony targets build from the current directory down
	if {[local-prefix] ne ""} {
		set parentprefix [make-prefix [file dirname [local-dir]]]
		foreach t {all clean distclean test} {
			Phony $t
			target $parentprefix$t -phony -depends [make-local $t]
		}
		clean-target [make-local clean] Clean
		clean-target [make-local distclean] DistClean
	}
}

proc BuildSpecEpilog {} {
	global tmake

	# Does subdir/all have any dependencies? If not, make it depend on subdir/libs (if that rule exists)
	array set t [get-target-rule [make-local all]]
	if {[llength $t(depends)] == 0} {
		if {[is-rule? [make-local libs]]} {
			Depends all [make-local libs]
		}
	}

	if {$tmake(localmakefile) ni {none -none}} {
		set makefile [make-local $tmake(localmakefile)]
		if {[info exists tmake(build)]} {
			# Create stub makefiles in srcdir, not builddir
			set makefile [file-join $tmake(topsrcdir) $makefile]
			# XXX: Makefiles will not be deleted by distclean here!
		}
		if {![file exists $makefile]} {
			set tmakeargs "\"--mflags=\$(MFLAGS)\" \"--gnumakeflags=\$(MAKEFLAGS)\" \"--bsdmakeflags=\$(.MAKEFLAGS)\""
			if {[info exists tmake(build)]} {
				append tmakeargs " --build=$tmake(build)"
			} else {
				# Set -C before -P
				if {[local-dir] ne "."} {
					append tmakeargs " -C [join [lrepeat [llength [split [local-dir] /]] ..] /]"
				}
				if {$tmake(topsrcdir) ne "."} {
					append tmakeargs " -P $tmake(topsrcdir)"
				}
			}
			append tmakeargs " SUBDIR=[local-dir]"

			set lines {}
			lappend lines "# Stub makefile generated by tmake v$tmake(version)"
			lappend lines ".PHONY: \$(.TARGETS) \$(MAKECMDGOALS) _all"
			lappend lines "_all:"
			lappend lines "\t+@$tmake(exe) $tmakeargs [local-prefix]$tmake(defaulttarget)"
			lappend lines "\$(.TARGETS) \$(MAKECMDGOALS):"
			lappend lines "\t+@$tmake(exe) $tmakeargs [local-prefix]\$@"
			writefile $makefile [join $lines \n]\n
		}
		DistClean $tmake(localmakefile)
	}
}

# ==================================================================
# HELPERS
# ==================================================================
proc expand-wildcard-braces {arglist} {
	set result {}
	foreach pattern $arglist {
		if {[string match {*{*}*} $pattern]} {
			regexp {^(.*)\{(.*)\}(.*)$} $pattern -> before alts after
			foreach i [split $alts ,] {
				lappend result $before$i$after
			}
		} else {
			lappend result $pattern
		}
	}
	#puts "$arglist => $result"
	return $result
}

# Returns a list of pairs: src dest
# The src is a local path, while the dest is relative.
# so ssl/*.h might return: {axtls/ssl/file.h ssl/file.h}
#
proc expand-wildcards {arglist {warn 0} {keepdir 1}} {
	set pairs {}
	foreach i $arglist {
		if {[string match {*[{}*?]*} $i]} {
			set count 0
			foreach ii [expand-wildcard-braces $i] {
				# ii=ssl/*.h
				# destdir=ssl
				set destdir [file dirname $ii]
				# srcdir=axtls/ssl
				if {$destdir eq "."} {
					set srcdir [local-dir]
				} else {
					set srcdir [make-local $destdir]
				}
				# globdir=../axtls/ssl
				set globdir [file-src $srcdir]
				if {![file isdir $globdir]} {
					puts "Warning: $globdir does not exist"
					continue
				}
				# pattern=*.h
				set pattern [file tail $ii]
				foreach j [readdir $globdir] {
					if {[string match $pattern $j]} {
						incr count
						# Need to return axtls/ssl/crypto.h ssl/crypto.h
						if {$keepdir} {
							lappend pairs [file-join $srcdir $j] [file-join $destdir $j]
						} else {
							lappend pairs [file-join $srcdir $j] [file tail $j]
						}
					}
				}
			}
			if {$warn && $count == 0} {
				puts "Warning: No matches for [make-local $i]"
			}
		} elseif {[string match *=* $i]} {
			lassign [split $i =] target src
			lappend pairs [make-local $src] $target
		} elseif {$i ne ""} {
			if {$keepdir} {
				lappend pairs [make-local $i] $i
			} else {
				lappend pairs [make-local $i] [file tail $i]
			}
		}
	}
	#puts "expand-wildcards [make-local $arglist] => $pairs"
	return $pairs
}

proc Glob {args} {
	#getopt {--tail args} args
	set result {}
	foreach pattern $args {
		foreach {i j} [expand-wildcards $pattern] {
			lappend result $j
		}
	}
	lsort $result
}

# ==================================================================
# HIGH LEVEL RULES
# ==================================================================

proc Executable {args} {
	show-this-rule
	getopt {--test --nobuild --publish --strip --install: target args} args
	if {$publish} {
		# Revisit: --publish=newname?
		Publish bin $target
	}
	set localtarget [make-local $target]
	Link $target {*}[Objects {*}[join $args]] {*}[get-define LOCAL_LIBS]
	if {[info exists install]} {
		set flags bin
		if {$strip} {
			lappend flags strip
		}
		InstallFile [file-join $install $target] $localtarget $flags
	}
	if {$test} {
		Test --chdir $target
	} elseif {!$nobuild} {
		Phony all $localtarget
	}
}

# Link an executable from objects
proc Link {target args} {
	show-this-rule

	define? CCLD [get-define CC]

	# Capture the current value of CCLD, LD_FLAGS and SYSLIBS
	target [make-local $target] -inputs {*}$args -depends [get-define PROJDEPS] -msg {note Link $target} -getvars CCLD LD_FLAGS PROJLIBS SYSLIBS -do {
		run $CCLD $LD_FLAGS $LDFLAGS -o $target $inputs $PROJLIBS $SYSLIBS
	}
	Clean $target
}

proc publish-one-file {dest src} {
	hard-link [file join [get-define PUBLISH] $dest] $src -vars dest $dest -msg {note Publish $dest}
}

proc make-shlib-symlinks {first args} {
	foreach alias $args {
		exec ln -s -f [file tail $first] $alias
	}
}

proc publish-shared-lib {names source} {
	set published [prefix [get-define PUBLISH]/lib/ $names]

	target $published -inputs $source -msg {note PublishSharedLib $libname}  -do {
		file delete $target
		exec ln -f $inputs [lindex $target 0]
		make-shlib-symlinks {*}$target
	} -vars libname [lindex $names 0]
	add-clean [make-local clean] $published
	add-clean [make-local distclean] $published
}

proc Publish {args} {
	show-this-rule
	getopt {--keepdir dir args} args

	foreach {src dst} [expand-wildcards $args 1 $keepdir] {
		publish-one-file $dir/$dst $src
	}
}

proc ArchiveLib {args} {
	show-this-rule
	getopt {--publish --install: basename args} args

	set libname lib$basename.a

	set target [make-local $libname]
	target $target -inputs {*}[Objects {*}[join $args]] -msg {note Ar $target} -do {
		file delete $target
		run $AR $ARFLAGS $target $inputs
		run $RANLIB $target
	}
	# Note: Libraries are not built by default, but are added to a local 'libs' rule
	Phony libs $target
	Clean $libname
	define-append LOCAL_LIBS $target

	if {[info exists install]} {
		InstallFile [file-join $install $libname] $target
	}
	if {$publish} {
		Publish lib $libname

		# Alias to make UseLibs work
		targetalias <lib>$basename [get-define PUBLISH]/lib/$libname
	}
}

proc SharedLib {args} {
	show-this-rule
	getopt {--publish --strip --version: --install: basename args} args

	if {![info exists version]} {
		set version 0
	}

	set n 0
	foreach part [split $version .] {
		if {![string is integer -strict $part]} {
			set n -1
			break
		}
		incr n
	}
	if {$n < 0 || $n > 3} {
		dev-error "Not a valid shared library version number: $version"
	}

	# What names may our shared lib have?
	set nameinfo [list [get-define SH_SOEXTVER] $version %s [get-define SH_SOEXT]]
	if {$n > 1} {
		lappend nameinfo [get-define SH_SOEXTVER] [lindex [split $version .] 0]
	}

	set names {}
	set libname {}

	foreach {fmt version} $nameinfo {
		set name lib$basename[format $fmt $version]
		if {$libname eq {}} {
			set libname $name
		}
		if {$name ni $names} {
			lappend names $name
		}
	}

	set locallibname [make-local $libname]
	set srcs [join $args]
	ObjectCFlags $srcs [get-define SH_CFLAGS]

	# Note: We won't link against local archive libs (LOCAL_LIBS) since they have not been compiled -fpic
	# The SharedLibLink rule includes a run-time check for linking against archive libs
	SharedLibLink $libname {*}[Objects {*}$srcs]

	# Only the fully versioned named is built in the local dir
	Phony libs $locallibname
	Clean $libname

	# Note: --publish is dummy. Shared libs always need to be published to be used
	incr publish

	if {$publish} {
		# Publish along with the various symlinks
		publish-shared-lib $names $locallibname
		# Alias to make UseLibs work
		targetalias <lib>$basename [get-define PUBLISH]/lib/$libname

		# Local executables only link against this lib if it is published
		# The reason is that it gets difficult to manage LD_LIBRARY_PATH or equalivalent.
		# If the shared lib is published, a single definition of LD_LIBRARY_PATH=$PUBLISH/lib will work for all shared libs
		# Thus UseLibs here rather than adding to LOCAL_LIBS

		UseLibs $basename
	}
	if {[info exists install]} {
		set flags bin
		if {$strip} {
			if {![define-exists STRIPLIB]} {
				dev-error "No \$STRIPLIB defined for this platform"
			}
			lappend flags striplib
		}
		InstallFile [file-join $install $libname] $locallibname $flags
	}
}

# If desired, this can be aliased to SharedLib in project.spec
alias Lib ArchiveLib

proc SharedObject {args} {
	show-this-rule

	getopt {--install: sharedobj args} args

	set srcs [join $args]

	# Must build objects with -fpic, etc.
	ObjectCFlags $srcs [get-define SHOBJ_CFLAGS]
	SharedObjectLink $sharedobj {*}[Objects {*}$srcs]

	set sharedobj [make-local $sharedobj]

	Phony all $sharedobj
	if {[info exists install]} {
		Install --bin $install $sharedobj
	}
}

# Link a dynamically loadable shared object from objects
proc SharedObjectLink {target args} {
	show-this-rule
	# Note that we only link against local shared libs, not archive libs
	target [make-local $target] -inputs {*}$args -msg {note SharedObject $target} -do {
		run $CC $SHOBJ_LDFLAGS -o $target $inputs $SYSLIBS
	}
	Clean $target
}

proc check-no-archive-libs {target args} {
	foreach i $args {
		set realname [map-alias $i]
		if {[string match *.a $realname]} {
			build-error "Error: Shared Lib $target may not link against Archive Lib $realname"
			return -code break
		}
	}
}

# Link a shared library from objects
proc SharedLibLink {target args} {
	show-this-rule

	set setsoname {}
	if {[get-define SH_SOPREFIX ""] ne ""} {
		set setsoname [get-define SH_SOPREFIX][file tail $target]
	}

	define? CCLD [get-define CC]

	# XXX: Here we use $LDFLAGS to capture any user-specied link flags (.e.g. -mbig-endian).
	#      Assume that these are required both for shared libs and for executables.
	#      Is it necessary to separate these two?
	#      Also, is LinkFlags/LD_FLAGS also required?

	target [make-local $target] -inputs {*}$args -depends [get-define PROJDEPS] -msg {note SharedLib $target} -getvars CCLD PROJLIBS SYSLIBS -do {
		check-no-archive-libs $target {*}$depends
		run $CCLD $SH_LDFLAGS $LDFLAGS -o $target $setsoname $inputs $PROJLIBS $SYSLIBS
	} -vars setsoname $setsoname
	Clean $target
}

# Create an object file from each source file
# Uses $OBJSRULES(.ext) to determine the build rule
# Returns a list of objects
# Accepts object files (.o) in addition to source files
# and simply returns them
proc Objects {args} {
	show-this-rule
	set objs {}
	foreach src $args {
		lappend objs [Object [change-ext .o $src] $src]
	}
	return $objs
}

proc Object {target src} {
	show-this-rule
	set ext [file ext $src]
	set obj [make-local $target]
	set src [make-local $src]
	if {$ext ne ".o"} {
		set extra {}
		if {[info commands ObjectRule$ext] eq ""} {
			dev-error "Don't know how to build Object from $src"
		}
		ObjectRule$ext $obj $src
		Clean $target
	}
	return $obj
}

# Add object-specific CFLAGS
proc ObjectCFlags {srcs args} {
	show-this-rule
	foreach src $srcs {
		set obj [change-ext .o $src]
		target [make-local $obj] -vars C_FLAGS [join $args]
	}
}

proc CFlags {args} {
	show-this-rule
	define-append C_FLAGS {*}$args
}

proc C++Flags {args} {
	define-append CXX_FLAGS {*}$args
}

proc LinkFlags {args} {
	define-append LD_FLAGS {*}$args
}

proc Linker {linker} {
	define CCLD $linker
}

proc UseLibs {args} {
	# Create/append to PROJLIBS (the link line)
	if {[get-define PROJLIBS] eq ""} {
		define PROJLIBS "-L[get-define PUBLISH]/lib"
	}
	define-append PROJLIBS [prefix -l $args]

	# Now the dependencies
	define-append PROJDEPS [prefix <lib> $args]
}

proc IncludePaths {args} {
	show-this-rule
	foreach dir $args {
		foreach prefix [list [local-dir] [top-src-dir]] {
			set dir [file-join $prefix $dir]
			if {$dir ni [get-define INCPATHS]} {
				define-append INCPATHS $dir
				CFlags [prefix -I $dir]
				C++Flags [prefix -I $dir]
			}
		}
	}
}

proc load-config-file {filename} {
	# All lines look like one of:
	#CONFIG_BOOL_VALUE=y (or m)
	#CONFIG_STR_VALUE="string value"
	#CONFIG_INT_VALUE=123
	set n 0
	foreach line [split [readfile $filename] \n] {
		incr n
		set line [string trim $line]
		if {$line eq "" || [string match "#*" $line]} {
			continue
		}
		if {[regexp {^([^=:]*):?=(.*)} $line -> name value]} {
			set value [string trim $value]
			if {$value eq "y"} {
				set value 1
			} elseif {[string match {"*"} $value]} {
				set value [lindex $value 0]
			}
			#puts "define $name $value"
			define $name $value
		} else {
			puts "$filename:$n: Warning: Failed to parse $line"
		}
	}
}

proc Load {args} {
	show-this-rule

	getopt {--nocare --config filename} args
	# XXX: Should we allow loading an out-of-tree file?
	set filename [make-local $filename]
	if {[file exists $filename]} {
		if {$config} {
			load-config-file $filename
		} else {
			source $filename
		}
		# If we find the file, we now care about it
		set nocare 0
	}
	lappend ::tmake(projfiles) $filename $nocare
}

proc UseSystemLibs {args} {
	define-append SYSLIBS {*}$args
}

proc PublishIncludes {args} {
	getopt {--keepdir args} args

	foreach {src dst} [expand-wildcards $args 1 $keepdir] {
		publish-one-file include/$dst $src
	}
}

proc setup-test-env {} {
	# Set up LD_LIBRARY_PATH for tests
	if {[incr ::tmake(setuptestenv)] == 1} {
		set ldlibpath [get-define LD_LIBRARY_PATH]
		if {$ldlibpath ne ""} {
			set path [getenv $ldlibpath ""]
			# Must use a full path here since tests do chdir
			set addlib [file-join [get-define builddir] [get-define PUBLISH]/lib]
			append-with-space path $addlib $::tcl_platform(pathSeparator)
			set ::env($ldlibpath) $path
		}
	}
}

proc Test {args} {
	show-this-rule

	getopt {--interp: --chdir --quiet command args} args
	set testid test#[incr ::tmake(testid)]
	set depends {}
	set command $command
	set localcommand [make-local $command]

	set opts {}

	if {$chdir} {
		# XXX: Will -chdir mess up the path to interp and command?
		lappend opts -chdir
	}
	if {!$quiet} {
		lappend opts -msg [list note "Test $localcommand"]
	}

	set testcommand {}
	if {[info exists interp]} {
		lassign [find-project-bin $interp $chdir] dep filename
		lappend opts -depends $dep
		lappend testcommand $filename 
	}
	lassign [find-project-bin $command $chdir] dep filename
	lappend opts -depends $dep
	lappend testcommand $filename

	lappend testcommand {*}$args

	target [make-local $testid] -phony -vars testcommand $testcommand {*}$opts -do {
		setup-test-env
		incr ::tmake(testruncount)
		run $testcommand
		incr ::tmake(testpasscount)
	}
	Phony test [make-local $testid]

	return $testid
}

proc CopyFile {dest source} {
	show-this-rule
	target [make-local $dest] -inputs [make-local $source] -msg "note Copy $dest" -do {
		file delete $target
		file copy $inputs $target
	}
	Clean $dest
}

proc hard-link {args} {
	show-this-rule

	getopt {--fallback dest source args} args

	# XXX: If the platform doesn't support hard links
	# and --fallback is set, fall back to soft links
	# and then to file copy
	# Want to depend on the existence of the dir, not the time
	target $dest -inputs $source -do {
		file delete $target
		exec ln $inputs $target
	} {*}$args
	add-clean [make-local clean] $dest
	add-clean [make-local distclean] $dest
}

# Helper for installing files
proc install-file {target source flags} {
	vputs "Copy $source $target"
	file copy -force $source $target
	if {"bin" in $flags} { chmodx-file $target }
	if {"strip" in $flags} { strip-file $target }
}

proc chmodx-file {target} {
	vputs "Chmod $target"
	exec chmod +x $target
}

proc strip-file {target} {
	vputs "Strip $target"
	exec [get-define STRIP true] $target
}

proc striplib-file {target} {
	vputs "StripLib $target"
	exec [get-define STRIPLIB false] $target
}

proc InstallFile {dest src {flags {}}} {
	show-this-rule

	#puts "InstallFile $dest $src"

	set destfile [get-define DESTDIR]$dest
	if {[is-target? $destfile]} {
		user-notice [warning-location "Warning: Duplicate install rule for $dest at [make-source-location $destfile]"]
		return
	}
	target $destfile -nocache -inputs $src -vars dest $dest flags $flags -msg {note "Install $dest"} -do {
		install-file $target $inputs $flags
		if {"bin" in $flags} { chmodx-file $target }
		if {"strip" in $flags} { strip-file $target }
		if {"striplib" in $flags} { striplib-file $target }
	}
	target install -depends $destfile

	# This file also needs to be uninstalled
	add-clean uninstall $destfile
}

proc Install {args} {
	show-this-rule

	getopt {--bin --strip --keepdir destdir args} args
	set flags {}
	if {$bin} {
		lappend flags bin
	}
	if {$strip} {
		lappend flags strip
	}

	set srcs {}
	foreach {src dst} [expand-wildcards $args 1 $keepdir] {
		lappend srcs $src
		InstallFile [file-join $destdir $dst] $src $flags
	}
	return $srcs
}

proc remove-empty-directories {files} {
	set dirs {}
	foreach file $files {
		while {1} {
			set dirname [file dirname $file]
			if {$dirname eq $file || $dirname eq "."} {
				break
			}
			dict set dirs [file dirname $file] 1
			set file $dirname
		}
	}
	if {[dict size $dirs]} {
		# Sort in reverse order so that abc/def/ghi is removed before abc/def
		#dputs m "file delete {*}[lsort -decreasing [dict keys $dirs]]"
		foreach dir [lsort -decreasing [dict keys $dirs]] {
			catch { file delete $dir }
		}
	}
}

# Helper to clean files (and any empty directories)
proc clean-files {files {msg {}}} {
	if {[llength $files]} {
		vputs "rm $files"
		if {$msg ne ""} {
			note "Clean [llength $files] $msg"
		}
		file delete {*}$files
		# Also need to remove any empty directories
		remove-empty-directories $files
	}
}

# This creates the clean target of the given type, e.g. clean, distclean
#
proc clean-target {type msg} {
	target $type -phony -nofail -vars cleanfiles {} -msg "note $msg [local-dir]" -do {
		clean-files $cleanfiles
	}
}

proc Clean {args} {
	if {[lindex $args 0] in {clean distclean}} {
		dev-error "fix call to Clean"
	}
	add-clean [make-local clean] [make-local {*}[join $args]]
	# DistClean also cleans these files
	DistClean {*}$args
}

proc DistClean {args} {
	add-clean [make-local distclean] [make-local {*}[join $args]]
}

# This adds files to be cleaned for the given type
#
proc add-clean {type filelist} {
	# This could be done with Depends or target, but it is much
	# faster to simply append to the rule variable directly
	#Depends [make-local $type] -vars cleanfiles [join $args]
	target-add-var $type cleanfiles $filelist
}

# Returns a list of {dependency filename}
#
proc find-project-bin {bin {chdir 0}} {
	set localbin [make-local $bin]
	if {[is-target? $localbin]} {
		dputs m "[find-source-location]: find-project-bin: $localbin is a local target"
		if {$chdir} {
			return [list $localbin $bin]
		} else {
			return [list $localbin [local-dir]/$bin]
		}
	} elseif {[file-src-exists $localbin]} {
		dputs m "[find-source-location]: find-project-bin: $localbin exists as source"
		if {$chdir} {
			# cd to local build dir, but file is relative to the top of the src dir
			set src [file-src-relative $localbin [file dirname $localbin]]
			return [list $localbin ./$src]
		} else {
			return [list $localbin [file-src $localbin]]
		}
	} else {
		dputs m "[find-source-location]: find-project-bin: $bin assumed to be published"
		set publishbin [get-define PUBLISH]/bin/$bin
		if {$chdir} {
			# cd to local build dir, but file is relative to top of the build dir
			set src [relative-path $publishbin [file dirname $localbin]]
			return [list $localbin $src]
		} else {
			return [list $publishbin $publishbin]
		}
	}
}

proc Generate {args} {
	# XXX: Would be nice if script and inputs were optional
	getopt {--interp: target script inputs rules args} args

	set localtarget [make-local {*}$target]

	set opts {}
	if {[info exists interp]} {
		lassign [find-project-bin $interp] dep filename
		lappend opts -depends $dep -vars interp $filename
	}
	if {$script ne ""} {
		lassign [find-project-bin $script] dep filename
		lappend opts -depends $dep -vars script $filename
	}
	target $localtarget -inputs {*}[make-local {*}$inputs] {*}$opts -do $rules -msg {note Generate $target} {*}$args
	Clean $target
}

proc Depends {target args} {
	target [make-local {*}$target] -depends {*}$args
}

proc Phony {target args} {
	show-this-rule
	target [make-local {*}$target] -phony -depends {*}$args
}

proc LocalMakefile {name} {
	# XXX Should be an API for this
	set ::tmake(localmakefile) $name
}

proc DefaultTarget {name} {
	set ::tmake(defaulttarget) $name
}

# XXX: Only useful for simple cases of building a host executable from C sources
# 
proc HostExecutable {target args} {
	target [make-local $target] -inputs {*}[make-local {*}$args] -getvars HOSTCFLAGS -do {
		run $CC_FOR_BUILD $HOSTCFLAGS -o $target $inputs
	}
	Publish bin $target
	Clean $target
}

proc add-publish-include {} {
	# Can't use IncludePaths here because we only want build paths
	set dir [get-define PUBLISH]/include
	if {$dir ni [get-define INCPATHS]} {
		define-append INCPATHS $dir
		CFlags [prefix -I $dir]
		C++Flags [prefix -I $dir]
	}
}

# XXX: Use an official API for this
lappend ::tmake(deferred) {
	add-publish-include
}
# ==================================================================
# Built-in targets
# ==================================================================

clean-target clean Clean
clean-target distclean DistClean
clean-target uninstall Uninstall

Phony all
Phony libs
Phony install all
Phony test

Depends all
Depends clean clean-orphans
Depends distclean clean-orphans
Depends test clean-orphans

Phony clean-orphans -do {
	clean-files [get-orphan-targets] "orphan targets"
	discard-orphan-targets
}

