# This is a combined login/logout page
#
# Note that it has 'auth none' to allow all users
# to access it

title "System Login"

auth none
setmode edit
storage none

help -nosearch {Login by entering your username and password}

init {
	package require auth
}

text {
	str {<div class="infobox">Login with your username and password for this device.</div>}
}

field username {
	label "Username"

	display {
		# Set focus to this field on load
		html str script type=text/javascript {
			function window_set_focus() {
				document.form.username.focus();
			}
			window.onload = window_set_focus;
		}
	}
}

# This hidden field "catches" the actual target page
# in case of an auth failure
#
# This is used to redirect to the target page once
# auth succeeds.
#
# Note that a default value is used in case the user navigates directly
# to this page.
#
field nextpage {
	storage page
	default overview
	editmode hidden
}

field password {
	label "Password"
	type password blank
	# Always clear this field before displaying
	display {
		cgi set $field ""
	}
}

button submit {
	label "Login"

	submit -tcl {
		set username [cgi get username]
		set password [cgi get password]
		if {[cgi auth authenticate $username $password]} {
			# Yes, password is OK, so create a session and set a cookie

			# Set 1 hour expiry for normal users and 1 week for super user
			set expiry 3600
			if {$username eq "root"} {
				set expiry 604800
			}

			cgi cookie set sessionid [auth_create_session $expiry username $username ipaddr [cgi getenv REMOTE_ADDR]]
		}

		# Now recheck auth. This will lookup the session info based on the sessionid cookie
		cgi auth reauth

		if {![cgi auth status]} {
			# The password must be wrong
			cgi error "The supplied credentials are not correct"
		} else {
			# Already authed (must have come here manually), so redirect
			cgi http header Location [cgi get nextpage]
			cgi http response 302
		}

		# Reset the action field
		cgi set action 0
	}
}

# This allows the logout toolbar to provide a logout
# link by using the url: login?action=1
submit -onget {
	if {[cgi get action] eq "1"} {
		# Delete the session if authentic. Ignore if not.
		auth_check_session delete [cgi cookie get sessionid] username [cgi auth username] ipaddr [cgi getenv REMOTE_ADDR]
		cgi cookie set sessionid ""
		cgi success "You are now Logged out"

		# And reset the action field
		cgi set action 0
	} elseif {[cgi auth status]} {
		# Already authed (must have come here manually), so redirect
		cgi http header Location [cgi get nextpage]
		cgi http response 302
	}
}

# Hidden field. If this is 1, we are logging out.
field action {
	editmode hidden
	storage page
	default 0
}
