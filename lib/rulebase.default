# These are the built-in rules
#
# They can be replaced if necessary

# ==================================================================
# Default variable settings and rules
# ==================================================================

# XXX: If settings are loaded via Load, these should all be ignored

define? CCACHE ""
define? CC cc
define? CCLD cc
define? CC_FOR_BUILD cc
define? CXX c++
define? AR ar
define? RANLIB ranlib
define? ARFLAGS cr
define? SH_LINKFLAGS ""
define? LOCAL_LIBS ""
define? DESTDIR ""
define? OBJCFLAGS ""
define? INCPATHS .

# These are normally specified by the user, either with configure
# or overridden on the command line or the environment
define? CFLAGS ""
define? CXXFLAGS ""
define? LDFLAGS ""

# These are set by the project via CFlags, ObjectCFlags, LinkFlags, etc.
set C_FLAGS ""
set CXX_FLAGS ""
set LD_FLAGS ""

# XXX Should be $TOP/publish
set PUBLISH publish

# XXX: Either reuse cc-shared.tcl or expect these to be set
#      in settings.conf
#
define? SH_CFLAGS -dynamic
define? SH_LDFLAGS "-dynamiclib"
define? SHOBJ_CFLAGS "-dynamic -fno-common"
define? SHOBJ_LDFLAGS "-bundle -undefined dynamic_lookup"

set PROJLIBS ""
set SYSLIBS ""

proc ObjectRule.c {obj src} {
	# Capture the current value of C_FLAGS and INCPATHS
	target $obj -inputs $src -msg {note Cc $target} -getvars C_FLAGS INCPATHS -do {
		run $CCACHE $CC $C_FLAGS $CFLAGS -c $inputs -o $target
	} -dyndep {
		header-scan-regexp-recursive $INCPATHS $HDRPATTERN
	}
}

proc ObjectRule.cpp {obj src} {
	# Capture the current value of CXX_FLAGS and INCPATHS
	target $obj -inputs $src -msg {note C++ $target} -getvars CXX_FLAGS INCPATHS -do {
		run $CCACHE $CXX $CXX_FLAGS $CXXFLAGS -c $inputs -o $target
	} -dyndep {
		header-scan-regexp-recursive $INCPATHS $HDRPATTERN
	}
}

set HDRPATTERN {^[\t ]*#[\t ]*include[\t ]*[<\"]([^\">]*)[\">]}

set SHAREDOBJRULE {run $CC $SHOBJ_LDFLAGS -o $target $inputs $SYSLIBS}

lappend tmake(subdirvars) CFLAGS CXXFLAGS LDFLAGS PROJLIBS SYSLIBS INCPATHS

# ==================================================================
# PROLOG/EPILOG HOOKS
# ==================================================================
proc BuildSpecProlog {} {
	# Local phony targets build from the current directory down
	if {[local-prefix] ne ""} {
		foreach t {all clean distclean test} {
			Phony $t [make-local $t]
			Phony [make-local $t]
		}
		CleanTarget clean
		CleanTarget distclean
	}
}

proc BuildSpecEpilog {} {
	if {$::tmake(localmakefile) ni {none -none}} {
		set makefile [make-local $::tmake(localmakefile)]
		if {![file exists $makefile]} {
			writefile $makefile "_all:\n\t+@$::tmake(exe) \$(MFLAGS) $::tmake(prefix)all\n\n\
\$(.TARGETS) \$(MAKECMDGOALS):\n\t+@$::tmake(exe) \$(MFLAGS) $::tmake(prefix)\$@\n"
		}
		Clean distclean $makefile
	}
}

# ==================================================================
# HIGH LEVEL RULES
# ==================================================================

proc Executable {args} {
	show-this-rule
	getopt {--test --publish --install: target args} args
	if {$publish} {
		# Revisit: --publish=newname?
		Publish bin $target
	}
	set target [make-local $target]
	Link $target {*}[Objects {*}[join $args]] {*}$::LOCAL_LIBS {*}$::PROJLIBS
	if {[info exists install]} {
		Install --bin $install $target
	}
	if {$test} {
		Test $target
	} else {
		Phony [make-local all] $target
	}
}

# Link an executable from objects
proc Link {target args} {
	show-this-rule

	# Capture the current value of CCLD, LD_FLAGS and SYSLIBS
	target $target -inputs {*}$args -msg {note Link $target} -getvars CCLD LD_FLAGS SYSLIBS -do {
		run $CCLD $LD_FLAGS $LDFLAGS -o $target $inputs $SYSLIBS
	}
	Clean clean $target
}

proc Publish {dir args} {
	show-this-rule
	foreach t [make-local $args] {
		set dest [file join $dir [file tail $t]]
		HardLink [file join $::PUBLISH $dest] $t -vars dest $dest -msg {note Publish $dest}
	}
}

proc ArchiveLib {args} {
	show-this-rule
	getopt {--publish --install: basename args} args

	set libname lib$basename.a

	set target [make-local $libname]
	target $target -inputs {*}[Objects {*}[join $args]] -msg {note Ar $target} -do {
		run $AR $ARFLAGS $target $inputs
		run $RANLIB $target
	}
	Phony [make-local all] $target
	Clean clean $target
	define-append LOCAL_LIBS $target

	if {[info exists install]} {
		Install $install $target
	}
	if {$publish} {
		Publish lib $libname
	}
}

alias Lib ArchiveLib

proc SharedObject {args} {
	show-this-rule

	getopt {--install: sharedobj args} args

	set sharedobj [make-local $sharedobj]

	# XXX: Should build objects with -fpic, etc.
	# Use -vars/ObjectCFlags to do this
	SharedObjectLink $sharedobj {*}[Objects {*}[join $args]]
	Phony [make-local all] $sharedobj
	if {[info exists install]} {
		Install --bin $install $sharedobj
	}
}

# Link an executable from objects
proc SharedObjectLink {target args} {
	show-this-rule
	# Note that we only link against local shared libs, not archive libs
	target $target -inputs {*}$args -do $::SHAREDOBJRULE -msg {note SharedObject $target}
	Clean clean $target
}

# Create an object file from each source file
# Uses $OBJSRULES(.ext) to determine the build rule
# Returns a list of objects
# Accepts object files (.o) in addition to source files
# and simply returns them
proc Objects {args} {
	show-this-rule
	set objs {}
	foreach src $args {
		lappend objs [Object [change-ext .o $src] $src]
	}
	return $objs
}

proc Object {obj src} {
	show-this-rule
	set ext [file ext $src]
	set obj [make-local $obj]
	set src [make-local $src]
	if {$ext ne ".o"} {
		set extra {}
		if {[info procs ObjectRule$ext] eq ""} {
			dev-error "Don't know how to build Object from $src"
		}
		ObjectRule$ext $obj $src
		Clean clean $obj
	}
	return $obj
}

# Add object-specific CFLAGS
proc ObjectCFlags {srcs args} {
	show-this-rule
	foreach src $srcs {
		set obj [change-ext .o $src]
		target [make-local $obj] -vars C_FLAGS [join $args]
	}
}

proc CFlags {args} {
	show-this-rule
	define-append C_FLAGS {*}$args
}

proc C++Flags {args} {
	define-append CXX_FLAGS {*}$args
}

proc LinkFlags {args} {
	define-append LD_FLAGS {*}$args
}

proc Linker {linker} {
	define CCLD $linker
}

proc UseLibs {args} {
	foreach lib $args {
		# REVISIT: If we are to support linking against project shared libs, PROJLIBS
		#          needs to be just a list of libs which will then be resolved to actual
		#          targets (archive or shared) at deferred resolution time
		define-append PROJLIBS [file join $::PUBLISH lib lib$lib.a]
	}
}

proc IncludePaths {args} {
	show-this-rule
	set paths [make-local {*}$args]
	define-append INCPATHS $paths
	CFlags [prefix -I $paths]
	C++Flags [prefix -I $paths]
}

proc Load {args} {
	getopt {--required filename} args
	set filename [make-local $filename]
	if {![file exists $filename]} {
		if {$required} {
			user-error "$filename does not exist, do you need run ./configure?"
		}
		puts "Warning: $filename does not yet exist"
	} else {
		uplevel #0 [list source $filename]
	}
	# Add a build dependency
	lappend ::tmake(projfiles) $filename
}

proc UseSystemLibs {args} {
	define-append SYSLIBS {*}$args
}

proc PublishIncludes {args} {
	foreach a [join $args] {
		Publish include $a
	}
}

proc Test {args} {
	getopt {--runwith: command args} args
	set testid test:[incr ::testid]
	set depends {}
	# XXX: If cross compiling, the existence of $runwith
	#      and $command as targets is no guarantee that they
	#      can run.
	set maybe-depends $command
	if {[info exists runwith]} {
		set testcommand "$runwith $command [join $args]"
		lappend maybe-depends $runwith
	} else {
		set testcommand "./$command [join $args]"
	}
	# Note: We don't yet know if $runwith and/or $command are targets, so defer until all rules are read.
	# 
	Phony $testid -maybe-depends {*}${maybe-depends} -vars testcommand $testcommand command $command -msg {note "Test $command"} -do {
		incr ::tmake(testruncount)
		run {*}$testcommand
		incr ::tmake(testpasscount)
	}
	Phony [make-local test] $testid

	return $testid
}

proc HardLink {args} {
	show-this-rule

	getopt {--fallback dest source args} args

	# XXX: If the platform doesn't support hard links
	# and --fallback is set, fall back to soft links
	# and then to file copy
	target $dest -inputs $source -do {
		file mkdir [file dirname $target]
		exec ln $inputs $target
	} {*}$args
	Clean clean $dest
}

# Helper for installing files
proc install-file {target source bin} {
	file mkdir [file dirname $target]
	vputs "Copy $source $target"
	file copy -force $source $target
	if {$bin} {
		vputs "chmod +x $target"
		exec chmod +x $target
	}
}

proc InstallFile {dest src {bin 0}} {
	show-this-rule

	set destfile $::DESTDIR$dest
	target $destfile -inputs $src -vars dest $dest bin $bin -msg {note "Install $dest"} -do {
		install-file $target $inputs $bin
	}
	Depends install $destfile

	# This file also needs to be uninstalled
	Clean uninstall $destfile

	#if {[dict exists $::tmake(install) $dest]} {
	#	user-notice "Warning: Duplicate install rule for $dest"
	#}
}

proc Install {args} {
	show-this-rule

	getopt {--bin --keepdir dest args} args

	set srcs {}
	foreach i $args {
		if {[string match {*[*?]*} $i]} {
			set flist [glob $i]
		} elseif {[string match *=* $i]} {
			lassign [split $i =] src target
			lappend srcs $src
			InstallFile [file join $dest $target] [make-local $src] $bin
			continue
		} else {
			set flist $i
		}
		foreach j $flist {
			lappend srcs $j
			if {$keepdir} {
				InstallFile [file join $dest $j] [make-local $j] $bin
			} else {
				InstallFile [file join $dest [file tail $j]] [make-local $j] $bin
			}
		}
	}
}

# This creates the clean target of the given type, e.g. clean, distclean
#
proc CleanTarget {type} {
	Phony [make-local $type] -nofail -vars cleanfiles {} -do {
		note "Clean $target"
		if {[llength $cleanfiles]} {
			vputs "rm $cleanfiles"
			file delete {*}$cleanfiles
		}
	}
}

# This adds files to be cleaned for the given type
#
proc Clean {type args} {
	# This could be done with Depends or target, but it is much
	# faster to simply append to the rule variable directly
	#Depends [make-local $type] -vars cleanfiles [join $args]
	target-add-var [make-local $type] cleanfiles [join $args]
}

proc Generate {target script inputs rules} {
	# XXX: Would be nice if script and inputs were optional
	set target [make-local {*}$target]
	if {$script ne ""} {
		set localscript [make-local $script]
		if {[is-target? $localscript] || [file exists $localscript]} {
			set script $localscript
		} else {
			# A non-local generator script must be in publish/bin
			set script [file join $::PUBLISH bin $script]
		}
	}
	target $target -inputs {*}[make-local {*}$inputs] -depends $script -vars script $script -do $rules -msg {note Generate $target}
	Clean clean $target
}

proc Depends {target args} {
	target $target -depends {*}$args
}

proc Phony {target args} {
	show-this-rule
	Depends $target -phony -depends {*}$args
}

proc LocalMakefile {name} {
	set ::tmake(localmakefile) $name
}

# ==================================================================
# Built-in targets
# ==================================================================

CleanTarget clean
CleanTarget distclean
CleanTarget uninstall

Depends clean clean-orphans
Depends distclean clean

Phony all
Phony install all
Phony test -do {
	# XXX: Need this to be a "run-anyway" command so that even if some tests fail it will still run
	# Actually, tests should not be targets. 
	puts [format "Test Summary: %d of %d passed" $tmake(testpasscount) $tmake(testruncount)]
}

Phony clean-orphans -do {
	set cleanfiles [get-orphan-targets]
	if {[llength $cleanfiles]} {
		note "Clean [llength $cleanfiles] orphan targets"
		file delete {*}$cleanfiles
		discard-orphan-targets
	}
}

# XXX: Use an official API for this
lappend tmake(deferred) {IncludePaths $PUBLISH/include}
